plugins {
	id 'java'
	id 'application'
	id 'org.graalvm.buildtools.native' version '0.9.19'

	id "com.diffplug.spotless" version "6.12.0"

	id "io.freefair.lombok" version "6.6.1"
}

group = 'com.github.eowiz'
version = '0.0.1-SNAPSHOT'

application {
	mainClass = 'com.github.eowiz.kozo.Main'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// picocli
	implementation 'info.picocli:picocli:4.7.0'
	annotationProcessor 'info.picocli:picocli-codegen:4.7.0'

	// Jackson
	implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
	implementation 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.14.1'

	// Test
	testImplementation 'org.junit:junit-bom:5.9.1'
	testImplementation 'org.assertj:assertj-core:3.23.1'
}

compileJava {
	options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	ratchetFrom 'origin/main'

	java {
		toggleOffOn()
		importOrder('java|javax', 'com.github.eowiz', '', '\\#com.github.eowiz', '\\#')
		removeUnusedImports()
		formatAnnotations()

		googleJavaFormat()
	}
}