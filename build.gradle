plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'org.hibernate.build.maven-repo-auth' version '3.0.4'
	id 'org.graalvm.buildtools.native' version '0.9.19'

	id "com.diffplug.spotless" version "6.14.0"

	id "io.freefair.lombok" version "6.6.3"
}

group = 'com.github.eowiz'
version = '0.0.1-SNAPSHOT'

application {
	mainClass = 'com.github.eowiz.kozo.Main'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	manifest {
		attributes 'Main-Class': application.mainClass
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

repositories {
	mavenCentral()

	maven {
		name = 'github'
		url = 'https://maven.pkg.github.com/eowiz/pebble'
	}
	maven { url 'https://jitpack.io' }
}

dependencies {
	// picocli
	implementation 'info.picocli:picocli:4.7.1'
	annotationProcessor 'info.picocli:picocli-codegen:4.7.1'

	// JSON Schema
	implementation 'com.github.saasquatch:json-schema-inferrer:0.1.5'

	// Pebble
	implementation 'com.github.eowiz:pebble:0.0.1-SNAPSHOT'

	// Logger
	implementation 'org.slf4j:slf4j-api:2.0.6'
	implementation 'ch.qos.logback:logback-core:1.4.5'
	implementation 'ch.qos.logback:logback-classic:1.4.5'

	// Test
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
	testImplementation 'org.assertj:assertj-core:3.24.2'
}

compileJava {
	options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

tasks.named('test') {
	useJUnitPlatform()

	jvmArgs '-agentlib:native-image-agent=access-filter-file=src/test/resources/access-filter.json,config-output-dir=ml'
}

graalvmNative {
	binaries {
		main {
			buildArgs.add('--no-fallback')
		}
		test {
			buildArgs.add('--no-fallback')
		}
	}
}

spotless {
	ratchetFrom 'origin/main'

	java {
		toggleOffOn()
		importOrder('java|javax', 'com.github.eowiz', '', '\\#com.github.eowiz', '\\#')
		removeUnusedImports()
		formatAnnotations()

		googleJavaFormat()
	}
}